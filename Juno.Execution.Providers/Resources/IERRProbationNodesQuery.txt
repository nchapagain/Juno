//Replace nodes with the real nodes we get from Tony. These are for testing purposes.
let nodes = dynamic(['0244af2c-1b80-4d9a-9e7e-0dc4abb5d3b0','0bc1507c-f961-4f58-9be2-6932add5619f','1181df9e-8bfc-412b-9a36-a6b680f5cc5e','172240d5-7aca-45fa-9c0a-d2aa0deb6be9','2d38cae8-eaf7-4d78-bf69-c54fd74ce4f0','4ead002b-7476-418f-accb-bc809ab430d9','53fe0ee0-e225-4ff7-9ee2-0528d77666b1','82034b5e-a83f-46a6-a68f-a5f96e4d9f8c','841787a5-9ee5-4e1c-9409-cbab9ad2cc8d','9122c883-e193-4c14-a1f7-de73fa86e0b3','98239349-057d-4fc8-bf3d-548c961daec4','a22243dd-1cb7-444d-a095-03a0058a3320','ada93580-f5a8-476a-9003-de4131ebfc37','af9688e2-68d3-467a-8c11-66ea6cdea13e','bbe079f2-2b1f-43bf-bb15-0c28d2a0b435','bf531810-9c03-425c-a208-df08f1979fef','c8744317-9c96-44c6-9dac-84a79e3779fb','ca4b1a67-5f69-4085-8284-91e62a4ee062','12867104-c570-4609-9813-e19c99a30020','5c70ec8e-baf9-45e6-9013-bc8c09492255','f8a4b611-511b-4adc-b5bf-1458fb85ca24']);
let beginTime = ago(2h);
// Step 1: get node's clusterId
cluster('azurecm.kusto.windows.net').database('AzureCM').LogNodeSnapshot
| where TIMESTAMP > beginTime and tipNodeSessionId == "00000000-0000-0000-0000-000000000000" and nodeState == "Ready" and containerCount == 0 
| where nodes has nodeId
| distinct nodeId, Region
// Step 2: get the rack location and machine pool name
| join cluster('azuredcmkpi.westus2.kusto.windows.net').database('AzureDCMDb').dcmInventoryGenerationMappingV3 on $left.nodeId == $right.NodeId
| summarize NodeList=make_list(nodeId) by RackLocation, MachinePool, ClusterId, Region
// Step 3: get external region names
| join kind=leftouter cluster('cirrus.kusto.windows.net').database('cirrus').Region on $left.Region == $right.FabricRegionName
// Step 4: get container policy for allowed VMs
| join kind=inner hint.strategy=broadcast (cluster('azurecm.kusto.windows.net').database('AzureCM').LogContainerPolicySnapshot
| where TIMESTAMP > beginTime
| distinct  policyInstanceName, machinePoolName
) on $left.MachinePool == $right.machinePoolName
// Step 5: get VMSKU mapping to external names
| join kind=inner hint.strategy=broadcast (cluster('cirrus.kusto.windows.net').database('cirrus').VMSKU
    | distinct FabricVMSkuName, VMSKUName
) on $left.policyInstanceName == $right.FabricVMSkuName
| extend RemainingTipSessions=array_length(NodeList)
| extend NodeList=tostring(NodeList)
| distinct RackLocation, ClusterId, RegionName, NodeList, MachinePool, VMSKUName, RemainingTipSessions
| summarize VMSKUs=make_list(VMSKUName) by MachinePool, RackLocation, RegionName, tostring(NodeList), ClusterId, RemainingTipSessions
| project RackLocation, ClusterId, CPUID="50654", Region=RegionName, RemainingTipSessions, SupportedVMSku=VMSKUs, NodeList, MachinePoolName=MachinePool